//This is the main build script for jAlarms. It builds the subprojects included in settings.gradle.
apply plugin: 'maven'

description='A simple to use alarm system for Java server applications. With jAlarms you can notify different users through different means about certain events in your application, so they can receive notice via email, MSN, SMS, Twitter, etc.'

dependsOnChildren()
defaultTasks 'build'

//Use the Java plugin on all projects, and set the same version and group.
allprojects {
	apply plugin:'java'
	version='1.5.3-SNAPSHOT'
	group='net.sf.jalarms'
}

//Here we set common stuff for all subprojects: the Maven repos, exclude commons-logging (use jcl-over-slf4j in UT),
//set some common dependencies
subprojects {
	repositories {
		mavenLocal()
		mavenCentral()
	}
	configurations {
		all*.exclude group:'commons-logging', module:'commons-logging'
	}
	dependencies {
		compile 'org.slf4j:slf4j-api:1.6.1'
		testCompile 'junit:junit:4.8.2'
		testRuntime 'org.slf4j:slf4j-simple:1.6.1', 'org.slf4j:jcl-over-slf4j:1.6.1'
	}
	tasks.javadoc.options.use=true
	tasks.javadoc.options.links=[
		'http://download.oracle.com/javase/6/docs/api/', 'http://slf4j.org/apidocs/',
		'http://static.springsource.org/spring/docs/3.0.x/javadoc-api/',
		'http://jalarms.sourceforge.net/core/apidocs/'
	]
	task javadocJar(type:Jar, dependsOn:'javadoc') {
		from javadoc.destinationDir
		classifier='javadoc'
	}
	task sourcesJar(type:Jar) {
		from sourceSets.main.allSource
		classifier='sources'
	}
	artifacts {
		archives javadocJar
		archives sourcesJar
	}
	//We will add the subproject's artifacts to the root project so they will be uploaded to Sonatype
	configurations.archives.artifacts.each { armatoste -> rootProject.configurations.archives.addArtifact armatoste }
}
tasks.jar.enabled=false

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'de.huxhorn.gradle:de.huxhorn.gradle.pgp-plugin:0.0.3'
	}
}

uploadArchives {
	repositories.mavenDeployer {
		configuration=configurations.archives
		repository(url: "http://oss.sonatype.org/service/local/staging/deploy/maven2/") {
			authentication(userName:nexusUsername, password:nexusPassword)
		}
		pom.project {
			name 'jAlarms - A simple alarm notification system for Java'
			packaging 'jar'
			description 'A simple to use alarm system for Java server applications. With jAlarms you can notify different users through different means about certain events in your application, so they can receive notice via email, MSN, SMS, Twitter, etc.'
			url 'http://jalarms.sourceforge.net/'
			inceptionYear '2009'
			scm {
				url 'http://jalarms.svn.sourceforge.net/viewvc/jalarms/trunk/jalarms'
				connection 'scm:svn:https://jalarms.svn.sourceforge.net/svnroot/jalarms/trunk/jalarms'
				developerConnection 'scm:svn:https://jalarms.svn.sourceforge.net/svnroot/jalarms/trunk/jalarms'
			}
			licenses {
				license {
					name 'GNU Lesser General Public License, version 2.1'
					url 'http://www.gnu.org/licenses/lgpl-2.1.html'
					distribution 'repo'
				}
			}
			developers {
				developer {
					id 'chochos'
					name 'Enrique Zamudio Lopez'
					email 'chochos@users.sourceforge.net'
					url 'http://javamexico.org/blogs/ezamudio'
					roles {
						role 'Architect'
						role 'Developer'
					}
					properties {
						twitter 'chochosmx'
					}
					timezone 'America/Mexico_City'
				}
			}
		}
	}
}
apply plugin: de.huxhorn.gradle.pgp.PgpPlugin
pgp {
	secretKeyRingFile = new File("${System.properties['user.home']}/.gnupg/secring.gpg")
	keyId=pgpKeyId
	password=pgpKeyPassphrase
}
