//This is the main build script for jAlarms. It builds the subprojects included in settings.gradle.
apply plugin: 'maven'
description='A simple to use alarm system for Java server applications. With jAlarms you can notify different users through different means about certain events in your application, so they can receive notice via email, MSN, SMS, Twitter, etc.'
version='1.6.1-SNAPSHOT'
groovyVersion='1.8.4'
slf4jVersion='1.6.3'
springVersion='3.0.6.RELEASE'

/*buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		mavenRepo name: "Gradle", urls: "http://repo.gradle.org/gradle/plugins-snapshots/"
	}
	dependencies {
		classpath 'org.gradle.plugins:gradle-signing-plugin:0.0.1-SNAPSHOT'
	}
}*/

allprojects {
	repositories {
		mavenLocal()
		mavenCentral()
	}
}

//Here we set common stuff for all subprojects: the Maven repos, exclude commons-logging (use jcl-over-slf4j in UT),
//set some common dependencies
subprojects { subproj->
	version=parent.version
	apply plugin:'java'
	apply plugin:'maven'
	apply plugin:'project-report'
	sourceCompatibility=6
	targetCompatibility=6
	group='net.sf.jalarms'

	configurations {
		all*.exclude group:'commons-logging', module:'commons-logging'
		deployerJars
		published.extendsFrom archives, signatures
	}

	dependencies {
		compile "org.slf4j:slf4j-api:${slf4jVersion}"
		testCompile 'junit:junit:4.10'
		testRuntime "org.slf4j:slf4j-simple:${slf4jVersion}", "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
		deployerJars 'org.apache.maven.wagon:wagon-http-lightweight:1.0-beta-6'
	}
	tasks.javadoc.options.use=true
	tasks.javadoc.options.links=[
		'http://download.oracle.com/javase/6/docs/api/', 'http://slf4j.org/apidocs/',
		'http://static.springsource.org/spring/docs/3.0.x/javadoc-api/',
		'http://jalarms.sourceforge.net/core/apidocs/'
	]

	task javadocJar(type:Jar, dependsOn:'javadoc') {
		from javadoc.destinationDir
		classifier='javadoc'
	}
	task sourcesJar(type:Jar, dependsOn:classes) {
		from sourceSets.main.allSource
		classifier='sources'
	}
	artifacts {
		archives javadocJar
		archives sourcesJar
	}

	apply plugin:'signing'
	signing {
		sign configurations.archives
	}

	uploadPublished {
		repositories {
			project.deployer=repositories.mavenDeployer {
				beforeDeployment { MavenDeployment deployment ->
					def signedPomArtifact=signPom(deployment)
					//signedPomArtifact.type="pom."+signing.type.extension
					deployment.addArtifact(signedPomArtifact)
				}
				repository(url:'http://oss.sonatype.org/service/local/staging/deploy/maven2/') {
					authentication(userName:nexusUsername, password:nexusPassword)
				}
		pom.project {
			name 'jAlarms - A simple alarm notification system for Java'
			packaging 'jar'
			description subproj.description
			url 'http://jalarms.sourceforge.net/'
			inceptionYear '2009'
			scm {
				url 'http://jalarms.svn.sourceforge.net/viewvc/jalarms/trunk/jalarms'
				connection 'scm:svn:https://jalarms.svn.sourceforge.net/svnroot/jalarms/trunk/jalarms'
				developerConnection 'scm:svn:https://jalarms.svn.sourceforge.net/svnroot/jalarms/trunk/jalarms'
			}
			licenses {
				license {
					name 'GNU Lesser General Public License, version 2.1'
					url 'http://www.gnu.org/licenses/lgpl-2.1.html'
					distribution 'repo'
				}
			}
			developers {
				developer {
					id 'chochos'
					name 'Enrique Zamudio Lopez'
					email 'chochos@users.sourceforge.net'
					url 'http://javamexico.org/blogs/ezamudio'
					roles {
						role 'Architect'
						role 'Developer'
					}
					properties {
						twitter 'chochosmx'
					}
					timezone 'America/Mexico_City'
				}
			}
		}


			}
		}
	}
}

//Only for parent project
configurations {
	published.extendsFrom archives, signatures
}

subprojects.each {
	project.evaluationDependsOn(it.name)
}

/*task clean(type: Delete) {
  delete buildDirName
}*/

task generateRelease(dependsOn: subprojects.uploadPublished, 
  group: "Release artifact",
  description: "Generates and uploads a final release to Sonatype Nexus")

