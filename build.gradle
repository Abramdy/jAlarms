//This is the main build script for jAlarms. It builds the subprojects included in settings.gradle.
apply plugin: 'maven'
description='A simple to use alarm system for Java server applications. With jAlarms you can notify different users through different means about certain events in your application, so they can receive notice via email, MSN, SMS, Twitter, etc.'
version='1.6.2'
ext.groovyVersion='1.8.8'
ext.slf4jVersion='1.7.1'
ext.springVersion='3.1.3.RELEASE'

/*buildscript {
	repositories {
		mavenCentral()
		mavenRepo name: "Gradle", urls: "http://repo.gradle.org/gradle/plugins-snapshots/"
	}
}*/

allprojects {
	repositories {
		mavenCentral()
	}
}

//Here we set common stuff for all subprojects: the Maven repos, exclude commons-logging (use jcl-over-slf4j in UT),
//set some common dependencies
subprojects { subproj->
	version=parent.version
	apply plugin:'java'
	apply plugin:'maven'
	apply plugin:'project-report'
	sourceCompatibility=7
	targetCompatibility=7
	group='net.sf.jalarms'

	configurations {
		all*.exclude group:'commons-logging', module:'commons-logging'
		deployerJars
		published.extendsFrom archives, signatures
	}

	dependencies {
		compile "org.slf4j:slf4j-api:${slf4jVersion}"
		testCompile 'junit:junit:4.11'
		testRuntime "org.slf4j:slf4j-simple:${slf4jVersion}", "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
		//deployerJars 'org.apache.maven.wagon:wagon-http-lightweight:1.0-beta-6'
	}
	tasks.javadoc.options.use=true
	tasks.javadoc.options.links=[
		'http://download.oracle.com/javase/7/docs/api/', 'http://slf4j.org/apidocs/',
		'http://static.springsource.org/spring/docs/3.1.x/javadoc-api/',
		'http://jalarms.sourceforge.net/core/apidocs/'
	]

	task javadocJar(type:Jar, dependsOn:'javadoc') {
		from javadoc.destinationDir
		classifier='javadoc'
	}
	task sourcesJar(type:Jar, dependsOn:classes) {
		from sourceSets.main.allSource
		classifier='sources'
	}
	artifacts {
		archives javadocJar
		archives sourcesJar
	}

	apply plugin:'signing'
	signing {
		sign configurations.archives
		required { gradle.taskGraph.hasTask('generateRelease') }
	}

	uploadArchives {
		repositories {
			project.ext.deployer=repositories.mavenDeployer {
				beforeDeployment { MavenDeployment deployment ->
					signing.signPom(deployment)
				}
				repository(url:'http://oss.sonatype.org/service/local/staging/deploy/maven2/') {
					authentication(userName:nexusUsername, password:nexusPassword)
				}
				pom.project {
					name 'jAlarms - A simple alarm notification system for Java'
					packaging 'jar'
					description subproj.description
					url 'http://jalarms.sourceforge.net/'
					inceptionYear '2009'
					scm {
						url 'http://github.com/chochos/jAlarms'
						connection 'scm:git://github.com/chochos/jAlarms.git'
						developerConnection 'scm:git@github.com:chochos/jAlarms.git'
					}
					licenses {
						license {
							name 'GNU Lesser General Public License, version 2.1'
							url 'http://www.gnu.org/licenses/lgpl-2.1.html'
							distribution 'repo'
						}
					}
					developers {
						developer {
							id 'chochos'
							name 'Enrique Zamudio Lopez'
							email 'chochos@users.sourceforge.net'
							url 'http://javamexico.org/blogs/ezamudio'
							roles {
								role 'Architect'
								role 'Developer'
							}
							properties {
								twitter 'chochosmx'
							}
							timezone 'America/Mexico_City'
						}
					}
				} //pom.project
			} //project.deployer
		} //repositories
	} //uploadPublished
} //subprojects

//Only for parent project
configurations {
	published.extendsFrom archives, signatures
}

subprojects.each {
	project.evaluationDependsOn(it.name)
}

task generateRelease(dependsOn: subprojects.uploadArchives, 
  group: "Release artifact",
  description: "Generates and uploads a final release to Sonatype Nexus")

